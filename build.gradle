// Execute Tasks in SubModule: gradle MODUL:clean build
plugins {
    id "de.freese.gradle.conventions" version "$version_myConventionPlugin" apply false
    id "io.spring.dependency-management" version "$version_springDependencyManagement" apply false
    id "name.remal.sonarlint" version "$version_nameRemalSonarlintPlugin" apply false
    id "de.freese.gradle.sonarlint-conventions" version "$version_myConventionPlugin" apply false
}

subprojects {
    apply plugin: "base"
    apply plugin: "de.freese.gradle.conventions"
    apply plugin: "io.spring.dependency-management"

    // name.endsWith('-doc')
    if (file("src/main/java").isDirectory()) {
        apply plugin: "name.remal.sonarlint"
        apply plugin: "de.freese.gradle.sonarlint-conventions"
    }

    dependencyManagement {
        imports {
//            mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
            mavenBom("org.springframework.boot:spring-boot-dependencies:$version_springBoot")
        }

        dependencies {
            dependency("dev.failsafe:failsafe:$version_failsafe")

            dependencySet(group: "com.sun.xml.bind:", version: "$version_jaxb") {
                entry "jaxb-xjc"
                entry "jaxb-impl"
            }
        }
    }

    // pluginManager.hasPlugin("java")
    pluginManager.withPlugin("java") {
        dependencies {
            //implementation(platform("org.springframework.boot:spring-boot-dependencies:$version_springBoot"))

            testImplementation("org.awaitility:awaitility")
            testImplementation("org.junit.jupiter:junit-jupiter")

            testRuntimeOnly("org.junit.platform:junit-platform-launcher")
        }
    }
}

// For Composite Build.
tasks.register("cleanAll", GradleBuild) {
    tasks = ["clean"]
}
tasks.register("buildAll", GradleBuild) {
    tasks = ["build"]
}
